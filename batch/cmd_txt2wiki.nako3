#
# command.txt を読んでWIKIの雛形を生成
#
プラグイン名＝空。
プラグイン拡張=空。
表題＝空。
基本パス＝母艦パスからパス抽出
目次＝「」
SQL=「」
TPL=「」
WIKI=""
PLUG={}
PLUG2={}
PLUG拡張={}
WIKI3=「■なでしこ3のプラグイン一覧」&改行&改行

「{母艦パス}/command.txt」を開いて、改行で区切る。
ここから反復
　　トリム
   Sはそれ。
   ＃プラグイン名か？
　　Sを「/^■([a-zA-Z0-9\-\_]+)\((.+)\)/」で正規表現マッチ。
　　もしそうならば
　　　　プラグイン名は、抽出文字列[0]
　　　　プラグイン拡張は、抽出文字列[1]
       目次＝目次＆改行＆「■ [[{プラグイン名}]] ([[{プラグイン拡張}]])」&改行
       目次＝目次＆「#page({プラグイン名}/解説)」＆改行
       PLUG[プラグイン名] = ""
       PLUG拡張[プラグイン名] = "[[拡張プラグイン]]"
　　　　続ける。
　　ここまで。
　　Sを「/^■(.+)/」で正規表現マッチ。
　　もしそうならば
　　　　プラグイン名は、抽出文字列[0]
　　　　プラグイン拡張は、空。
       目次＝目次＆改行＆「■ [[{プラグイン名}]]」&改行
       目次＝目次＆「#page({プラグイン名}/解説)」＆改行
       PLUG[プラグイン名] = ""
       PLUG拡張[プラグイン名] = "プラグイン"
　　　　続ける。
　　ここまで。
   ＃表題か？
　　Sを「/^●(.+)/」で正規表現マッチ。
　　もし、そうならば
　　　　表題は抽出文字列[0]
      # 目次＝目次＆「● [[{表題}:{プラグイン名}/{表題}]]」&改行
      目次＝目次＆改行&「● [[{表題}:{プラグイン名}]]」&改行
      PLUG[プラグイン名] = PLUG[プラグイン名] & "- [[{表題}:{プラグイン名}/{表題}]]"&改行
      PLUG2["{プラグイン名}/{表題}"] = ""
　　　　続ける。
　　ここまで。
   ＃コマンドか？
　　Sを「/^\|(.+)/」で正規表現マッチ。
　　もし、そうならば
　　　　CMDは抽出文字列[0]
　　　　CMDを「|」で区切る
　　　　C=それ
　　　　タイプ＝トリム(C[0])
　　　　名前＝トリム(C[1])
　　　　引数＝トリム(C[2])の「/」を「|」に置換
　　　　説明＝トリム(C[3])
　　　　ヨミ＝トリム(C[4])
       カナ1＝LEFT(ヨミ,1)
       ページ名＝「{プラグイン名}/{名前}」
       ページ解説名＝「{プラグイン名}/{名前}/解説」
       もし、タイプ=「関数」ならば
         もし、引数＝空ならば
         目次＝目次＆「- [[{名前}:{ページ名}]] --- {説明}」&改行
         違えば
           目次＝目次＆「- [[({引数}){名前}:{ページ名}]] --- {説明}」&改行
         ここまで。
         PLUG2["{プラグイン名}/{表題}"] = PLUG2["{プラグイン名}/{表題}"]&「- [[{名前}:{ページ名}]] --- {説明}」&改行
         ###
         WIKI=WIKI&「###PAGE###:w:{ページ名}」&改行
         WIKI=WIKI&「■ {名前} ({ヨミ})」&改行
         WIKI=WIKI&『{{{#csv(flag=||)』&改行
         WIKI=WIKI&「カテゴリ || [[{プラグイン名}]] > [[{表題}:{プラグイン名}/{表題}]]」&改行
         WIKI=WIKI&「種類 || {タイプ}」&改行
         WIKI=WIKI&「引数 || {引数}」&改行
         WIKI=WIKI&「説明 || {説明}」&改行
         WIKI=WIKI&『}}}』&改行&改行
         WIKI=WIKI&「#page({ページ解説名})」&改行&改行
         WIKI=WIKI&「▲ 参考」&改行
         WIKI=WIKI&「- [[命令一覧/機能順]] > [[{プラグイン名}]]」&改行
         WIKI=WIKI&「」&改行
         WIKI=WIKI&「###PAGE###:c:{ページ解説名}」&改行
         WIKI=WIKI&「●『[[{名前}:{ページ解説名}]]』の詳しい解説」&改行&改行
         WIKI=WIKI&「{説明}」&改行&改行
         WIKI=WIKI&『{{{』&改行
         WIKI=WIKI&『# 準備中』&改行
         WIKI=WIKI&『}}}』&改行&改行
      違えば
        ARG＝引数
        もし、引数≠空ならば、ARG=「({引数})」
        目次＝目次＆「- [[{ARG}{名前}:{ページ名}]] --- 定数」&改行
        PLUG2["{プラグイン名}/{表題}"] = PLUG2["{プラグイン名}/{表題}"]&「- [[{名前}:{ページ名}]] --- 定数」&改行
        ###
        WIKI=WIKI&「###PAGE###:w:{ページ名}」&改行
        WIKI=WIKI&「■ {名前} ({ヨミ})」&改行
        WIKI=WIKI&『{{{#csv(flag=||)』&改行
        WIKI=WIKI&「カテゴリ || [[{プラグイン名}]] > [[{表題}:{プラグイン名}/{表題}]]」&改行
        WIKI=WIKI&「種類 || {タイプ}」&改行
        WIKI=WIKI&「初期値 || {説明}」&改行
        WIKI=WIKI&『}}}』&改行&改行
        WIKI=WIKI&「#page({ページ解説名})」&改行&改行
        WIKI=WIKI&「▲ 参考」&改行
        WIKI=WIKI&「- [[命令一覧/機能順]] > [[{プラグイン名}]]」&改行
        WIKI=WIKI&「」&改行
        WIKI=WIKI&「###PAGE###:c:{ページ解説名}」&改行
        WIKI=WIKI&「●『[[{名前}:{ページ解説名}]]』の詳しい解説」&改行&改行
        WIKI=WIKI&『{{{』&改行
        WIKI=WIKI&『# 準備中』&改行
        WIKI=WIKI&『}}}』&改行&改行
      ここまで
　　ここまで。
ここまで。
「{母艦パス}/wiki-command.txt」にWIKIを保存。
「{母艦パス}/wiki-index.txt」に(トリム(目次)&改行)を保存。

#-----------------
#プラグインごとの関数名を追加
WIKI=""
PLUGを反復
  プラグイン名＝対象キー
  拡張 = PLUG拡張[プラグイン名]
  WIKI=WIKI&「###PAGE###:w:{プラグイン名}」&改行
  WIKI=WIKI&「■ {プラグイン名} - なでしこ3 {拡張}」&改行
  WIKI=WIKI&「#page({プラグイン名}/解説)」&改行&改行
  WIKI=WIKI&「▲ プラグインで使える命令のカテゴリ」&改行&改行
  WIKI=WIKI&トリム(対象)&改行&改行
  WIKI=WIKI&「▲ 参考」&改行
  WIKI=WIKI&「- [[命令一覧/機能順]] > [[プラグイン一覧]]」&改行
  WIKI=WIKI&改行
  WIKI=WIKI&「###PAGE###:c:{プラグイン名}/解説」&改行
  WIKI=WIKI&「[[{プラグイン名}:{プラグイン名}/解説]]とは、」
  WIKI=WIKI&「なでしこv3の{拡張}。」&改行&改行
  WIKI=WIKI&改行
  ###
  WIKI3=WIKI3&「▲ 🔌[[{プラグイン名}]] {拡張}」&改行
  WIKI3=WIKI3&「#page({プラグイン名}/解説)」&改行&改行
ここまで。
PLUG2を反復
  プラグイン表題名＝対象キー
  プラグイン表題名を「/」で区切る。
  プラグイン名＝それ[0]
  表題=それ[1]
  WIKI=WIKI&「###PAGE###:w:{プラグイン表題名}」&改行
  WIKI=WIKI&「■ [[{プラグイン名}]]/[[{表題}:{プラグイン表題名}]]」&改行
  WIKI=WIKI&「#page({プラグイン名}/解説)」&改行&改行
  WIKI=WIKI&「▲ カテゴリの命令一覧」&改行&改行
  WIKI=WIKI&トリム(対象)&改行&改行
  WIKI=WIKI&「▲ 参考」&改行
  WIKI=WIKI&「- [[命令一覧/機能順]] > [[プラグイン一覧]] > [[{プラグイン名}:{プラグイン名}]]」&改行
  WIKI=WIKI&改行
ここまで。
「{母艦パス}/wiki-command2.txt」にWIKIを保存。
「{母艦パス}/wiki-plugins-index.txt」にWIKI3を保存。



