!「../../src/plugin_express.js」を取り込む。
# ---------------------------------
# なでしこ3のユーザーフォルダ
# ---------------------------------
HOME=ホームディレクトリ取得
USER_DIR=「{HOME}/nadesiko3_user」
もし、!(USER_DIRのフォルダ存在)ならば
  USER_DIRにフォルダ作成
ここまで

# ---------------------------------
# WEBサーバ起動
# ---------------------------------
APPKEY = (65535の乱数)&「:」&(65535の乱数)&「:」&(65535の乱数)
PORT=3030
PORTでWEBサーバ起動
WEBサーバ起動成功した時には(PNO)
  「+- appkey={APPKEY}」を表示。
  「http://localhost:{PNO}」をブラウザ起動。
ここまで
WEBサーバ起動失敗した時には
  PORT = PORT + 1
  もし、PORT> 3050ならば
    「WEBサーバが起動できませんでした。」と表示。
    戻る。
  ここまで
  PORTでWEBサーバ起動
ここまで

# ---------------------------------
# 静的ディレクトリの指定
# ---------------------------------
「/html」を「{母艦パス}/html」へWEBサーバ静的パス指定
母艦パスを「../../release」で相対パス展開してRELEASEに代入。
「/release」をRELEASEへWEBサーバ静的パス指定。

# ---------------------------------
# ルーティング
# ---------------------------------
「/」へWEBサーバGETした時には
  「/html/files.html?appkey={APPKEY}」へWEBサーバリダイレクト。
ここまで

# ---------------------------------
「/test」へWEBサーバGETした時には
  「test」をWEBサーバ出力。
ここまで

# ---------------------------------
「/files」へWEBサーバGETした時には
  S=「{USER_DIR}/*.nako3」のファイル列挙
  SをJSONエンコードしてWEBサーバ出力。
ここまで

# ---------------------------------
「/load」へWEBサーバGETした時には
  GETデータを表示。
  FILE=GETデータ["file"]
  UKEY=GETデータ["appkey"]
  もし、UKEY≠APPKEYならば
    『### APPKEYが違います ###』とWEBサーバ出力。
  ここまで
  FILE=FILEの「..」を「」に置換して、「/」を「」に置換。
  FPATH=「{USER_DIR}/{FILE}」
  FPATHが存在
  もし、そうならば
    FPATHを開く。
    それをWEBサーバ出力。
  違えば
    『# --- ここにプログラムを記述 ---』&改行&『「こんにちは」と表示。』をWEBサーバ出力。
  ここまで。
ここまで。

# ---------------------------------
「/run」へWEBサーバPOSTした時には
  はいでサーバ保存実行処理。
ここまで

# ---------------------------------
「/save」へWEBサーバPOSTした時には
  いいえでサーバ保存実行処理。
ここまで

●サーバ保存実行処理(RUNで)
  FILE=POSTデータ["file"]
  BODY=POSTデータ["body"]
  UKEY=POSTデータ["appkey"]
  もし、UKEY≠APPKEYならば
    「エラー:APPKEYが違います」をWEBサーバ出力。
    戻る。
  違えば
  ここまで。
  もし、(FILE=undefined)または(FILE=空)ならば
    「エラー: ファイル名がありません」をWEBサーバ出力。
    戻る。
  ここまで
  FPATH=「{USER_DIR}/{FILE}」
  「FILE=」&FPATHを表示。
  FPATHへBODYを保存。
  FPATH=「{USER_DIR}/{FILE}」
  エラー監視
    FPATHへBODYを保存。
  エラーならば
    「NG」をWEBサーバ出力。
    戻る。
  ここまで。
  もし、RUNがはいならば
    エラー監視
      「RUN {FPATH}」と表示。
      RES=「cnako3 "{FPATH}"」を起動待機。
      「---」と表示。
      RESをWEBサーバ出力。
    エラーならば
      「[ERROR] {エラーメッセージ}」をWEBサーバ出力。
    ここまで
  違えば
    「OK」をWEBサーバ出力。
  ここまで。
ここまで。
